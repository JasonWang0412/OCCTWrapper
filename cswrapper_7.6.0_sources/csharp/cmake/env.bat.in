@ECHO OFF

:: Created: 2019-06-05

:: Copyright (c) 2019-2021 OPEN CASCADE SAS

:: This file is part of commercial software by OPEN CASCADE SAS.

:: This software is furnished in accordance with the terms and conditions
:: of the contract and with the inclusion of this copyright notice.
:: This software or any other copy thereof may not be provided or otherwise
:: be made available to any third party.

:: No ownership title to the software is transferred hereby.

:: OPEN CASCADE SAS makes no representation or warranties with respect to the
:: performance of this software, and specifically disclaims any responsibility
:: for any damages, special or consequential, connected with its use.

:: Directory of sample
set "SAMPLE_DIR=%~dp0"
set "SAMPLE_DIR=%SAMPLE_DIR:~0,-1%"

rem Unset environment variables that can remain from previous call
rem to env.bat and affect result of this call
set "BUILD_TYPE="
set "CASDEB="
set "CSHARP2COM=@USED_CSHARP2COM@"

:: Path to OCCT products directory containing OCCLicense component,
:: used in Protected configuration only
:: By default, assume we are building in standard products environment.
set "OCCLICENSE=%~dp0..\.."

:: Call local settings, if available
if exist "%SAMPLE_DIR%\custom.bat" (
  echo Setting local configuration from %SAMPLE_DIR%\custom.bat
  call "%SAMPLE_DIR%\custom.bat"
)

if not "%PRODROOT%" == "" (
  if exist "%PRODROOT%\env.bat" (
    echo Setting local configuration from %PRODROOT%\env.bat
    call "%PRODROOT%\env.bat" %1 %2 %3
  )
)

if "%ARCH%" == "" (
   if "%CASROOT%" == "" (
    :: Call env.bat from parent folder
    if exist "%~dp0..\..\env.bat" (
      echo Setting local configuration from %~dp0..\..\env.bat
      call "%~dp0..\..\env.bat" %1 %2 %3
    )
  ) else (
    if exist "%CASROOT%\env.bat" (
      echo Setting local configuration from %CASROOT%\env.bat
      call "%CASROOT%\env.bat" %1 %2 %3
    )
  )
)

:: Support specific configuration "Protected"
if "%BUILD_TYPE%" == "" (
  set BUILD_TYPE=Release
  if not "%3" == "" (
    set BUILD_TYPE=%3
  )
)
if /i "%BUILD_TYPE%" == "Protected" (
  set CASDEB=p
)

set "BIN_TAIL=win%ARCH%\%VCVER%\bin%CASDEB%"
set "PATH=%SAMPLE_DIR%\%BIN_TAIL%;%PATH%"
